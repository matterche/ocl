/**
 * <copyright>
 *
 * Copyright (c) 2010 E.D.Willink and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *     E.D.Willink - initial API and implementation
 *
 * </copyright>
 *
 * $Id: CompleteOCL.xtext,v 1.13 2011/05/21 14:55:54 ewillink Exp $
 */
grammar org.eclipse.ocl.examples.xtext.completeocl.CompleteOCL
with org.eclipse.ocl.examples.xtext.essentialocl.EssentialOCL

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "platform:/resource/org.eclipse.ocl.examples.xtext.base/model/BaseCST.ecore" as base
import "platform:/resource/org.eclipse.ocl.examples.xtext.essentialocl/model/EssentialOCLCST.ecore" as essentialocl
import "platform:/resource/org.eclipse.ocl.examples.pivot/model/Pivot.ecore" as pivot
import "platform:/resource/org.eclipse.ocl.examples.xtext.completeocl/model/CompleteOCLCST.ecore"
//generate completeOCL "http://www.eclipse.org/ocl/examples/xtext/completeocl/CompleteOCL"
CompleteOCLDocumentCS returns CompleteOCLDocumentCS:
	(ownedImport+=ImportCS | ownedInclude+=IncludeCS | ownedLibrary+=LibraryCS)*
	(packages+=PackageDeclarationCS | contexts+=ContextDeclCS)*;

terminal UNQUOTED_STRING:	// Never forward parsed; just provides a placeholder
	'£$%^£$%^'				//  for reverse serialisation of embedded OCL 
;

NavigationOperator:						// Intended to be overridden
	EssentialOCLNavigationOperator | '^' | '^^'
;

UnrestrictedName returns ecore::EString:
	EssentialOCLUnrestrictedName
	//| 'body'
	//| 'context'
	//| 'def'
	| 'derive'
	//|	'endpackage'
	| 'import'
	| 'include'
	| 'init'
	//| 'inv'
	| 'library'
	//|	'package'
	//|	'post'
	//|	'pre'
	//|	'static'
;

PrimitiveTypeIdentifier:
	'Boolean'
	| 'Integer'
	| 'Real'
	| 'String'
	| 'UnlimitedNatural'
	| 'OclAny'
	| 'OclInvalid'
	| 'OclMessage'
	| 'OclState'
	| 'OclVoid';

BodyCS returns BodyCS:
	stereotype='body' (name=UnrestrictedName)? ':' specification=SpecificationCS;

ClassifierContextDeclCS returns ClassifierContextDeclCS:
	'context' (selfName=UnrestrictedName)?
	pathName=PathNameCS
	(rules+=InvCS | rules+=DefCS)+;

ContextDeclCS returns ContextDeclCS:
	PropertyContextDeclCS
	| ClassifierContextDeclCS
	| OperationContextDeclCS;

DefCS returns DefCS:
	(static?='static')? stereotype='def' (name=UnrestrictedName)? ':' feature=(DefOperationCS|DefPropertyCS) '=' specification=SpecificationCS;

DefOperationCS returns DefOperationCS:
	name=UnrestrictedName '(' (parameters+=DefParameterCS (',' parameters+=DefParameterCS)*)? ')' ':' (ownedType=TypeExpCS)?;

DefParameterCS returns base::ParameterCS:
	name=UnrestrictedName ':' ownedType=TypeExpCS;

DefPropertyCS returns DefPropertyCS:
	name=UnrestrictedName ':' ownedType=TypeExpCS;

DerCS returns DerCS:
	stereotype='derive' ':' specification=SpecificationCS;

ImportCS returns base::ImportCS:
	'import' (name=Identifier ':')? namespace=[pivot::Namespace|URI] (all?='::' '*')?;

IncludeCS returns IncludeCS:
	'include' namespace=[pivot::Namespace|URI];

InitCS returns InitCS:
	stereotype='init' ':' specification=SpecificationCS;

InvCS returns InvCS:
	stereotype='inv' (name=UnrestrictedName ('(' messageSpecification=SpecificationCS ')')?)? ':' specification=SpecificationCS;

LibraryCS returns base::LibraryCS:
	'library' package=[pivot::Package|URI];

OperationContextDeclCS returns OperationContextDeclCS:
	'context' pathName=PathNameCS '(' 
	(parameters+=ParameterCS (',' parameters+=ParameterCS)*)?
	')' ':' (ownedType=TypeExpCS)?
	((rules+=PreCS) | (rules+=PostCS) | (rules+=BodyCS))*;

PackageDeclarationCS returns PackageDeclarationCS:
	'package' pathName=PathNameCS (contexts+=ContextDeclCS)* 'endpackage';

ParameterCS returns base::ParameterCS:
	(name=UnrestrictedName ':')? ownedType=TypeExpCS;

PostCS returns PostCS:
	stereotype='post' (name=UnrestrictedName)? ':' specification=SpecificationCS;

PreCS returns PreCS:
	stereotype='pre' (name=UnrestrictedName)? ':' specification=SpecificationCS;

PropertyContextDeclCS returns PropertyContextDeclCS:
	'context' pathName=PathNameCS ':' (ownedType=TypeExpCS)?
	((rules+=InitCS (rules+=DerCS)?)? | (rules+=DerCS (rules+=InitCS)?));

SpecificationCS returns ContextSpecificationCS:
	ownedExpression=ExpCS | exprString=UNQUOTED_STRING;

//---------------------------------------------------------------------------------
//	EssentialOCL overrides
//---------------------------------------------------------------------------------
//CollectionTypeCS returns essentialocl::CollectionTypeCS:
//	name=CollectionTypeIdentifier
//	('(' ownedType=TypeExpCS ')')?;

NavigatingArgExpCS returns essentialocl::ExpCS:
	({OclMessageArgCS} '?')
	| ExpCS
;

//TupleTypeCS returns base::TupleTypeCS:
//	name='Tuple'
//	('(' (ownedParts+=tuplePartCS (',' ownedParts+=tuplePartCS)*)? ')')?;
